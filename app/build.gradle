apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// This is needed for the navigation safe-args
apply plugin: 'androidx.navigation.safeargs'

// Firebase Crashlytics
apply plugin: 'io.fabric'

def appVersionCode = 1035
def appVersionName = "0.1.35"

// Required by Epoxy
kapt {
    correctErrorTypes = true
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.gpetuhov.android.hive"
        minSdkVersion 16
        targetSdkVersion 28
        multiDexEnabled true
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Required by FirebaseUI (without it dex merging fails)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def SEP = "_"
            def date = new Date()
            def formattedDate = date.format('dd.MM.YYYY_HH-mm')

            outputFileName = "hive" + SEP + variant.buildType.name + SEP + appVersionName + SEP + formattedDate + ".apk"
        }
    }

    // Required to use data binding
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    // Jetpack Navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigationVersion"

    // Anko
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"

    // Android KTX
    implementation 'androidx.core:core-ktx:1.0.1'

    // KAndroid
    implementation 'com.pawegio.kandroid:kandroid:0.8.8@aar'

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // Bottom Navigation View
    implementation "com.google.android.material:material:1.0.0"

    // Google Play Services
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Lottie
    implementation "com.airbnb.android:lottie:2.8.0"

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

    // Firebase core
    implementation 'com.google.firebase:firebase-core:16.0.6'

    // Firestore
    implementation 'com.google.firebase:firebase-firestore:17.1.4'

    // Firebase Cloud Storage
    implementation 'com.google.firebase:firebase-storage:16.0.5'

    // This must be explicitly set, otherwise the app will crash (used by FirebaseUI)
    implementation 'com.google.firebase:firebase-auth:16.1.0'

    // FirebaseUI for Firebase Auth
    implementation 'com.firebaseui:firebase-ui-auth:4.2.0'

    // Firebase Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'

    // Firebase Cloud Messaging
    implementation 'com.google.firebase:firebase-messaging:17.3.4'

    // Facebook (required for FirebaseUI)
    implementation 'com.facebook.android:facebook-android-sdk:4.37.0'

    // Twitter (required for FirebaseUI)
    implementation 'com.twitter.sdk.android:twitter-core:3.1.1'

    // Multidex (is required by Firebase)
    implementation 'com.android.support:multidex:1.0.3'

    // Material dialogs
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"
    implementation "com.afollestad.material-dialogs:input:$materialDialogsVersion"

    // Google Maps Utility
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    // Data Binding
    kapt 'androidx.databinding:databinding-compiler:3.4.0-alpha09'

    // Moxy
    implementation "com.arello-mobile:moxy:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    implementation "com.arello-mobile:moxy-android:$moxyVersion"
    kapt "com.arello-mobile:moxy-compiler:$moxyVersion"

    // Mockito
    testImplementation "org.mockito:mockito-core:2.23.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1"

    // GeoFirestore
    implementation 'com.github.imperiumlabs:GeoFirestore-Android:v1.1.1'

    // Android Lifecycle
    // Without this there will be "Unresolved reference: ViewModelProviders"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    kapt "android.arch.lifecycle:compiler:1.1.1"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.4"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    // AirBnb Epoxy
    implementation "com.airbnb.android:epoxy:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"
}

// Needed for Firebase
// Must be at the bottom of the file!
apply plugin: 'com.google.gms.google-services'
